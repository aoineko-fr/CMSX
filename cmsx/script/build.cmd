REM @echo off
"%__APPDIR__%chcp.com" 65001 > nul
title <nul & title CMSX Build Tool – %ProjName% – %Target%

setlocal EnableDelayedExpansion

rem -- Setup text color
set RESET=[0m
set RED=[91m
set GREEN=[92m
set YELLOW=[93m
set BLUE=[94m
set MAGENTA=[95m
set CYAN=[96m
set BG=[44m

cls
echo %BG%
echo ╔═══════════════════════════════════════════════════════════════════════════╗
echo ║                                                                           ║
echo ║  ██▀█▀██▀▀▀█▀▀█▀█  ▄▄▄ ▄▄                                                 ║
echo ║  █  ▄ █▄ ▀██▄ ▀▄█ ██   ██                                                 ║
echo ║  █  █ █▀▀ ▄█  █ █ ▀█▄█ ██▄▄                                               ║
echo ║  ▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀                                                         ║
echo ║   ▄▄▄       ▄  ▄▄    ▄▄   ▄▄▄▄           ▄▄                               ║
echo ║   ██▄▀ ██ █ ▄  ██   ▄██    ██  ▄█▀▄ ▄█▀▄ ██                               ║
echo ║   ██▄▀ ▀█▄█ ██ ▀█▄ ▀▄██    ██  ▀█▄▀ ▀█▄▀ ▀█▄                              ║
echo ║                                                                           ║
echo ╚═══════════════════════════════════════════════════════════════════════════╝
echo %RESET%

rem ***************************************************************************
rem * TARGET SETTINGS                                                         *
rem ***************************************************************************

rem -- Check MSX version
if /I %Version%==1			( echo » Version: MSX 1
) else if /I %Version%==2	( echo » Version: MSX 2
) else if /I %Version%==2P	( echo » Version: MSX 2+
) else if /I %Version%==TR	( echo » Version: MSX turbo R
) else if /I %Version%==12	( echo » Version: MSX 1/2
) else (
	echo %RED%Error: Unknow MSX Version%RESET%
	exit /B 10
)

rem -- Target specific initializations
call %LibDir%\script\setup_target.cmd

rem -- Create ctr0 config file
if defined Mapper (
	echo ; MSX-GL ^| Crt0 config file generated by the Build tool	>  %OutDir%\crt0_config.asm
	echo ROM_MAPPER=%Mapper%		>> %OutDir%\crt0_config.asm
	echo ADDR_BOOT=0x%StartAddr%	>> %OutDir%\crt0_config.asm
)

rem -- Overwrite RAM start address
if defined ForceRamAddr (
	echo Force RAM address to %ForceRamAddr%
	set RamAddr=%ForceRamAddr%
)

rem ***************************************************************************
rem * CHECK PARAMETERS                                                        *
rem ***************************************************************************

rem -- Check parameters
if not defined ProjName (
	echo %RED%Error: Invalide project name [%ProjName%]%RESET%
	exit /B 20
)

rem -- Check tools
if not exist %SDCC%\sdcc.exe (
	echo %RED%Error: Invalide path to C Compiler [%SDCC%\sdcc.exe]%RESET%
	exit /B 30
)
if not exist %SDCC%\sdasz80.exe (
	echo %RED%Error: Invalide path to ASM Compiler [%SDCC%\sdasz80.exe]%RESET%
	exit /B 40
)
if not exist %Hex2Bin% (
	echo %RED%Error: Invalide path to Hex2Bin [%Hex2Bin%]%RESET%
	exit /B 50
)
if not exist %FillFile% (
	echo %RED%Error: Invalide path to FillFile [%FillFile%]%RESET%
	exit /B 60
)
if not exist %Emulator% (
	echo %YELLOW%Warning: Invalide path to Emulator [%Emulator%]%RESET%
)
if not exist %Debugger% (
	echo %YELLOW%Warning: Invalide path to Debugger [%Debugger%]%RESET%
)
if not exist %DskTool% (
	echo %YELLOW%Warning: Invalide path to DskTool [%DskTool%]%RESET%
	echo Only programs in ROM format will be testable with most emulators 
)


rem ***************************************************************************
rem * MODULES                                                                 *
rem ***************************************************************************

rem  Add crt0 source to build list (it must be the first in the list)
set SrcList=%LibDir%\src\crt0\%Crt0%.asm
set LibList=%OutDir%\%Crt0%.rel 

rem  Add project sources to build list
if not defined ProjModules (
	echo %YELLOW%Warning: ProjModules not defined. Adding %ProjName% to build list.%RESET%
	set ProjModules=%ProjName%
)
for %%G in (%ProjModules%) do (
	if not exist %%G.c (
		echo %RED%Error: Source file %%G.c don't exist%RESET%
		exit /B 100
	)
	set SrcList=!SrcList!,%%G.c
	set LibList=!LibList! %OutDir%\%%~nG.rel
)

echo » Modules: %LibModules%

rem  Add modules sources to build list
for %%G in (%LibModules%) do (
	if not exist %LibDir%\src\%%G.c (
		echo %RED%Error: Module %%G don't exist%RESET%
		exit /B 110
	)
	set SrcList=!SrcList!,%LibDir%\src\%%G.c
	set LibList=!LibList! %OutDir%\%%~nG.rel
)

rem ***************************************************************************
if %DoClean%==0 goto :NoClean

echo.
echo ┌───────────────────────────────────────────────────────────────────────────┐
echo │ CLEAN                                                                     │
echo └───────────────────────────────────────────────────────────────────────────┘

if exist %OutDir% (
	echo Removing %OutDir%...
	rd /S /Q %OutDir% 
)
if exist %ProjDir%\emul (
	echo Removing \emul...
	rd /S /Q %ProjDir%\emul
)

:NoClean

rem ***************************************************************************
rem * INIT                                                                    *
rem ***************************************************************************
if not exist %OutDir% ( md %OutDir% )
if not exist %ProjDir%\emul ( md %ProjDir%\emul )
if /I %Ext%==rom (
	if not exist %ProjDir%\emul\rom ( md %ProjDir%\emul\rom )
)
if /I %Ext%==bin (
	if not exist %ProjDir%\emul\bin ( md %ProjDir%\emul\bin )
	if not exist %ProjDir%\emul\dsk ( md %ProjDir%\emul\dsk )
)
if /I %Ext%==com (
	if not exist %ProjDir%\emul\dos%DOS% ( md %ProjDir%\emul\dos%DOS% )
	if /I %DOS%==1 (
		if not exist %ProjDir%\emul\dos%DOS%\COMMAND.COM  ( copy /Y %MSXDOS%\COMMAND.COM %ProjDir%\emul\dos%DOS% )
		if not exist %ProjDir%\emul\dos%DOS%\MSXDOS.SYS   ( copy /Y %MSXDOS%\MSXDOS.SYS %ProjDir%\emul\dos%DOS% )
	)
	if /I %DOS%==2 (
		if not exist %ProjDir%\emul\dos%DOS%\COMMAND2.COM ( copy /Y %MSXDOS%\COMMAND2.COM %ProjDir%\emul\dos%DOS% )
		if not exist %ProjDir%\emul\dos%DOS%\MSXDOS2.SYS  ( copy /Y %MSXDOS%\MSXDOS2.SYS %ProjDir%\emul\dos%DOS% )
	)
	if not exist %ProjDir%\emul\dsk ( md %ProjDir%\emul\dsk )
)

rem ***************************************************************************
if %DoCompile%==0 goto :NoCompile

echo.
echo ┌───────────────────────────────────────────────────────────────────────────┐
echo │ COMPILE                                                                   │
echo └───────────────────────────────────────────────────────────────────────────┘

%SDCC%\sdcc.exe --version

call %LibDir%\script\compile_all.cmd
if errorlevel 1 goto :Error

:NoCompile

rem ***************************************************************************
if %DoMake%==0 goto :NoMake

echo.
echo ┌───────────────────────────────────────────────────────────────────────────┐
echo │ MAKE                                                                      │
echo └───────────────────────────────────────────────────────────────────────────┘

echo %BLUE%Making %ProjName% using SDCC...%RESET%

%SDCC%\sdcc.exe --version

if %Optim%==Speed (set LinkOpt=%LinkOpt% --opt-code-speed)
if %Optim%==Size (set LinkOpt=%LinkOpt% --opt-code-size)

set SDCCParam=-mz80 --no-std-crt0 --code-loc 0x%CodeAddr% --data-loc 0x%RamAddr% --constseg RODATA --vc %LinkOpt% %LibList% -o %OutDir%\
echo SDCC %SDCCParam%
%SDCC%\sdcc.exe %SDCCParam%
if errorlevel 1 goto :Error
echo %GREEN%Succeed%RESET%

:NoMake

rem ***************************************************************************
if %DoPackage%==0 goto :NoPackage

echo.
echo ┌───────────────────────────────────────────────────────────────────────────┐
echo │ PACKAGE                                                                   │
echo └───────────────────────────────────────────────────────────────────────────┘

rem ***************************************************************************
rem * HEX2BIN                                                                 *
rem ***************************************************************************
set H2BParam=-e %Ext% -s %StartAddr% %OutDir%\%Crt0%.ihx
echo %BLUE%Converting to binary...%RESET%

echo HEX2BIN %H2BParam%
%Hex2Bin% %H2BParam% 
if errorlevel 1 goto :Error
echo %GREEN%Succeed%RESET%

rem ***************************************************************************
rem * FILL                                                                    *
rem ***************************************************************************
if %FillSize%==0 goto :NoFill

echo %BLUE%Filling binary up to %FillSize% bytes...%RESET%
%FillFile% %OutDir%\%Crt0%.%Ext% %FillSize%
if errorlevel 1 goto :Error
echo %GREEN%Succeed%RESET%

:NoFill

rem ***************************************************************************
rem * MAPPER SEGMENTS                                                                    *
rem ***************************************************************************
if %MapperSize%==0 goto :NoMapper

set /A FirstSeg=%FillSize% / %SegSize%
set /A LastSeg=(%MapperSize% / %SegSize%) - 1
set /A Size8K=8*1024

echo %BLUE%Adding extra mapper segments [%FirstSeg%-%LastSeg%]...%RESET%
for /L %%I in (%FirstSeg%,1,%LastSeg%) do (
	echo %CYAN%-- Segment #%%I --%RESET%
	if exist %ProjName%_s%%I_b0.c (
		echo Found. Add %ProjName%_s%%I_b0.c in Bank #0 at %Bank0Addr%
		call :AddSegment %ProjName%_s%%I_b0.c %Bank0Addr%
	) else if exist %ProjName%_s%%I_b1.c (
		echo Found. Add %ProjName%_s%%I_b1.c in Bank #1 at %Bank1Addr%
		call :AddSegment %ProjName%_s%%I_b1.c %Bank1Addr%
	) else if exist %ProjName%_s%%I_b2.c (
		echo Found. Add %ProjName%_s%%I_b2.c in Bank #2 at %Bank2Addr%
		call :AddSegment %ProjName%_s%%I_b2.c %Bank2Addr%
	) else if exist %ProjName%_s%%I_b3.c (
		echo Found. Add %ProjName%_s%%I_b3.c in Bank #3 at %Bank3Addr%
		call :AddSegment %ProjName%_s%%I_b3.c %Bank3Addr%
	) else ( 
		echo Not Found. Add dummy data
		if %SegSize% EQU %Size8K% (
			copy /Y /B %OutDir%\%Crt0%.%Ext%+%ToolsDir%\MakeROM\rom_seg8k.bin %OutDir%\%Crt0%.%Ext%
		) else (
			copy /Y /B %OutDir%\%Crt0%.%Ext%+%ToolsDir%\MakeROM\rom_seg16k.bin %OutDir%\%Crt0%.%Ext%
		)
	)
)

goto :NoMapper

:AddSegment
	%SDCC%\sdcc.exe -mz80 --no-std-crt0 --code-loc 0x%2 --data-loc 0x%2 --vc %1 -o out\
	%Hex2Bin% -e %Ext% -s 0x%2 %OutDir%\%~n1.ihx
	%FillFile% %OutDir%\%~n1.%Ext% %SegSize%
	copy /Y /B %OutDir%\%Crt0%.%Ext%+%OutDir%\%~n1.%Ext% %OutDir%\%Crt0%.%Ext%
	exit /B 200

:NoMapper

:NoPackage

rem ***************************************************************************
if %DoDeploy%==0 goto :NoDeploy

echo.
echo ┌───────────────────────────────────────────────────────────────────────────┐
echo │ DEPLOY                                                                    │
echo └───────────────────────────────────────────────────────────────────────────┘

echo %BLUE%Deploying %Target%...%RESET%

for %%I in ("%DskTool%") do set DskToolPath=%%~dI%%~pI
for %%J in ("%DskTool%") do set DskToolName=%%~nJ%%~xJ

rem ***************************************************************************
if /I %Ext%==bin (
	echo -- Copy %OutDir%\%Crt0%.%Ext% to emul\bin\%ProjName%.%Ext%
	copy /Y %OutDir%\%Crt0%.%Ext% %ProjDir%\emul\bin\%ProjName%.%Ext%
	if errorlevel 1 goto :Error
	echo -- Create emul\bin\autoexec.bas
	echo 10 print"Loading..." > %ProjDir%\emul\bin\autoexec.bas
	echo 20 bload"%ProjName:~0,8%.%Ext%",r >> %ProjDir%\emul\bin\autoexec.bas
	rem ---- Generate DSK file ----
	if exist %DskTool% (
	
		echo %GREEN%Succeed%RESET%
		echo %BLUE%Generating DSK file...%RESET%
	
		echo -- Temporary copy files to DskTool directory
		copy /Y %ProjDir%\emul\bin\autoexec.bas %DskToolPath%
		copy /Y %ProjDir%\emul\bin\%ProjName%.%Ext% %DskToolPath%

		echo -- Generate .DSK file from autoexec.bas %ProjName%.%Ext%
		echo %DskToolName% a temp.dsk autoexec.bas %ProjName%.%Ext%
		set PrevCD=%cd%
		cd %DskToolPath%
		%DskToolName% a temp.dsk autoexec.bas %ProjName%.%Ext%
		cd !PrevCD!
		
		echo -- Copy DSK file to %ProjDir%\emul\dsk\%ProjName%.dsk
		copy /Y %DskToolPath%\temp.dsk %ProjDir%\emul\dsk\%ProjName%.dsk

		echo -- Clean temporary files
		del /Q %DskToolPath%\autoexec.bas %DskToolPath%\%ProjName%.%Ext% %DskToolPath%\temp.dsk
	)
)

rem ***************************************************************************
if /I %Ext%==rom (
	echo Copy %OutDir%\%Crt0%.%Ext% to emul\rom\%ProjName%.%Ext%
	copy /Y %OutDir%\%Crt0%.%Ext% %ProjDir%\emul\rom\%ProjName%.%Ext%
	if errorlevel 1 goto :Error
)

rem ***************************************************************************
if /I %Ext%==com (
	echo Copy %OutDir%\%Crt0%.%Ext% to emul\dos%DOS%\%ProjName%.%Ext%
	copy /Y %OutDir%\%Crt0%.%Ext% %ProjDir%\emul\dos%DOS%\%ProjName%.%Ext%
	if errorlevel 1 goto :Error
	echo Create emul\dos%DOS%\autoexec.bat
	if /I %DOS%==1 (
		echo %ProjName% > %ProjDir%\emul\dos%DOS%\autoexec.bat
	)
	if /I %DOS%==2 (
		echo echo Loading... > %ProjDir%\emul\dos%DOS%\autoexec.bat
		echo %ProjName%.%Ext% >> %ProjDir%\emul\dos%DOS%\autoexec.bat
		echo %ProjName% >> %ProjDir%\emul\dos%DOS%\autoexec.bat
	)
	
	if errorlevel 1 goto :Error
	rem ---- Generate DSK file ----
	if exist %DskTool% (

		echo %GREEN%Succeed%RESET%
		echo %BLUE%Generating DSK file...%RESET%

		if /I %DOS%==1 (
			set FilesList=COMMAND.COM MSXDOS.SYS autoexec.bat %ProjName%.%Ext%
		)
		if /I %DOS%==2 (
			set FilesList=COMMAND2.COM MSXDOS2.SYS autoexec.bat %ProjName%.%Ext%
		)
		
		echo -- Temporary copy files to DskTool directory
		for %%K in (!FilesList!) do ( copy /Y %ProjDir%\emul\dos%DOS%\%%K %DskToolPath% )

		echo -- Generate .DSK file
		echo %DskToolName% a temp.dsk !FilesList!
		set PrevCD=%cd%
		cd %DskToolPath%
		%DskToolName% a temp.dsk !FilesList!
		cd !PrevCD!

		echo -- Copy DSK file to %ProjDir%\emul\dsk\%ProjName%.dsk
		copy /Y %DskToolPath%\temp.dsk %ProjDir%\emul\dsk\%ProjName%.dsk

		echo -- Clean temporary files
		del /Q %DskToolPath%\temp.dsk
		for %%K in (!FilesList!) do ( del /Q %DskToolPath%\%%K )
	)
)

echo %GREEN%Succeed%RESET%

:NoDeploy

rem ***************************************************************************
if %DoRun%==0 goto :NoRun

echo.
echo ┌───────────────────────────────────────────────────────────────────────────┐
echo │ RUN                                                                       │
echo └───────────────────────────────────────────────────────────────────────────┘

rem ***************************************************************************
rem * EMULATOR                                                                *
rem ***************************************************************************
call %LibDir%\script\setup_emulator.cmd

:NoRun

REM ////////////////////////////////////////

echo %GREEN%Build Succeed%RESET%
exit /B 0

:Error

echo %RED%Error: Build Failed with error number %errorlevel%%RESET%
exit /B 666